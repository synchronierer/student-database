plugins {
    java
    application
    id("com.gradleup.shadow") version "9.6.1"
    id("com.vanniktech.maven.publish") version "0.37.0"
}

group = "io.github.learn-monitor"

version = "v2.0.1"

application {
    mainClass.set("de.igslandstuhl.database.Application")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.xerial:sqlite-jdbc:3.53.2.1")
    implementation("com.google.code.gson:gson:2.14.0")
    implementation("commons-codec:commons-codec:1.22.1")
    implementation("com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20260313.1")
    implementation("org.jline:jline:4.3.1") // for better console input handling

    // Logging
    implementation("org.slf4j:slf4j-api:2.0.18")
    implementation("ch.qos.logback:logback-classic:1.6.1")

    // built-in plugins
    implementation("io.github.learn-monitor:plugin-loader:v1.0.7")

    testImplementation("org.junit.jupiter:junit-jupiter:6.1.2") // using JUnit 5 (latest)
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

tasks.javadoc {
    classpath = sourceSets.main.get().compileClasspath
}
tasks.test {
    useJUnitPlatform()
    systemProperty("test.environment", "true")
}
tasks.jar {
    manifest {
        attributes["Main-Class"] = application.mainClass.get()
    }
}
tasks.shadowJar {
    archiveBaseName.set("student-database")
    archiveClassifier.set("fat")
    archiveVersion.set(project.version.toString())    // omit version in filename if you want
    manifest {
        attributes["Main-Class"] = application.mainClass.get()
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17)) // or another version you prefer
    }
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates(group.toString(), "student-database", version.toString())

    pom {
        name = "Student database"
        description = "A Java-based application designed to manage and store student information efficiently. It allows admins to perform CRUD (Create, Read, Update, Delete) operations on student records, classes, subjects, and other school-related data, making it a valuable tool for educational institutions. Students can view their progress, and teachers can assign them topics, based on subjects."
        url = "https://github.com/Learn-Monitor/student-database"

        licenses {
            license {
                name = "GNU General Public License v3.0"
                url = "http://www.gnu.org/licenses/gpl-3.0.txt"
            }
        }
        developers {
            developer {
                id = "schlaumeier5"
                name = "Lukas Morgenstern"
                url = "https://github.com/schlaumeier5"
            }
        }
        scm {
            url = "https://github.com/Learn-Monitor/student-database"
            connection = "scm:git:https://github.com/Learn-Monitor/student-database.git"
            developerConnection = "scm:git:ssh://git@github.com/Learn-Monitor/student-database.git"
        }
    }
}
tasks.withType<AbstractPublishToMaven>().configureEach {
    dependsOn(tasks.withType<Sign>())
}
publishing {
    repositories {
        maven {
            name = "snapshots"
            url = uri("https://maven.pkg.github.com/Learn-Monitor/student-database")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: findProperty("gpr.user") as String?
                password = System.getenv("GITHUB_TOKEN") ?: findProperty("gpr.key") as String?
            }
        }
    }
}

/**
 * Creates a release archive containing the regular JAR and the resource
 * directory used by FileResourceProvider.
 */
tasks.register<org.gradle.api.tasks.bundling.Zip>("releaseZip") {
    group = "distribution"
    description = "Bundles the regular JAR and external resource files."

    val regularJar = tasks.named<org.gradle.api.tasks.bundling.Jar>("jar")

    dependsOn(regularJar)

    archiveBaseName.set("student-database")
    archiveVersion.set(project.version.toString())
    archiveClassifier.set("resources")
    destinationDirectory.set(layout.buildDirectory.dir("distributions"))

    from(regularJar.flatMap { it.archiveFile })

    from("src/main/resources") {
        into("resources")
    }
}

// Existing API tests share singleton caches and assume fixture IDs start at 1.
// Run curriculum fixtures in their own JVM/database, still as part of `test`.
val curriculumTest by tasks.registering(Test::class) {
    description = "Runs isolated curriculum budget, ownership and migration tests."
    group = "verification"
    testClassesDirs = sourceSets.test.get().output.classesDirs
    classpath = sourceSets.test.get().runtimeClasspath
    include("**/curriculum/**")
    useJUnitPlatform()
    systemProperty("test.environment", "true")
    systemProperty("curriculum.test.environment", "true")
}
tasks.test {
    exclude("**/curriculum/**")
    dependsOn(curriculumTest)
}
